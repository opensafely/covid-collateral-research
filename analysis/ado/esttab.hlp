{smcl}
{* 23may2007}{...}
{hi:help esttab}{right:(SJ7-2: st0085_1; SJ5-3: st0085)}
{hline}

{title:Title}

{p 4 4 2}{hi:esttab} {hline 2} Display formatted regression table


{title:Table of contents}

    {help esttab##syn:Syntax}
    {help esttab##des:Description}
    {help esttab##opt:Options}
    {help esttab##exa:Examples}
    {help esttab##ret:Returned results}
    {help esttab##aut:Backmatter}


{marker syn}{...}
{title:Syntax}

{p 8 15 2}
{cmd:esttab} [ {it:namelist} ] [ {cmd:using} {it:filename} ] [ {cmd:,}
{it:options} ]


    where {it:namelist}  is    {cmd:_all} | {cmd:*} | {it:name} [{it:name} ...]
          {it:name}      is    {it:identifier} | {cmd:.}


    {it:options}{col 26}description
    {hline 70}
    {help esttab##main:Main}
      {cmd:b(}{it:{help esttab##fmt:fmt}}{cmd:)}{col 26}{...}
specify format for point estimates
      {cmd:beta}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]{col 26}{...}
display beta coefficients instead of point est's
      {cmd:main(}{it:name} [{it:{help esttab##fmt:fmt}}]{cmd:)}{col 26}{...}
display contents of {cmd:e(}{it:name}{cmd:)} instead of point e's
      {cmd:t(}{it:{help esttab##fmt:fmt}}{cmd:)}{col 26}{...}
specify format for t statistics
      {cmd:abs}{col 26}{...}
use absolute value of t statistics
      {cmd:not}{col 26}{...}
suppress t statistics
      {cmd:se}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]{col 26}{...}
display standard errors instead of t statistics
      {cmd:p}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]{col 26}{...}
display p-values instead of t statistics
      {cmd:ci}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]{col 26}{...}
display confidence intervals instead of t stat's
      {cmd:aux(}{it:name} [{it:{help esttab##fmt:fmt}}]{cmd:)}{col 26}{...}
display contents of {cmd:e(}{it:name}{cmd:)} instead of t stat's
      [{ul:{cmd:no}}]{cmdab:con:stant}{col 26}{...}
do not/do report the intercept

    {help esttab##sig:Significance asterisks}
      [{cmd:no}]{cmd:star}[{cmd:(}{it:list}{cmd:)}]{col 26}{...}
do not/do report significance asterisks
      {cmd:staraux}{col 26}{...}
attach asterisks to t stat's instead of point est's

    {help esttab##stat:Summary statistics}
      {cmd:r2}|{cmd:ar2}|{cmd:pr2}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]{col 26}{...}
display (adjusted, pseudo) R-squared
      {cmd:aic}|{cmd:bic}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]{col 26}{...}
display Akaike's or Schwarz's information crit.
      {cmdab:sca:lars:(}{it:list}{cmd:)}{col 26}{...}
display any other scalars contained in {cmd:e()}
      {cmd:sfmt(}{it:{help esttab##fmt:fmt}} [{it:...}]{cmd:)}{col 26}{...}
set format(s) for {cmd:scalars()}
      {cmd:noobs}{col 26}{...}
do not display the number of observations
      {cmd:obslast}{col 26}{...}
place the number of observations last

    {help esttab##layout:Layout}
      {cmd:wide}{col 26}{...}
place point est's and t stat's beside one another
      [{ul:{cmd:no}}]{cmdab:pa:rentheses}{col 26}{...}
do not/do print parentheses around t statistics
      {cmdab:br:ackets}{col 26}{...}
use brackets instead of parentheses
      [{ul:{cmd:no}}]{cmdab:gap:s}{col 26}{...}
suppress/add vertical spacing
      [{ul:{cmd:no}}]{cmdab:li:nes}{col 26}{...}
suppress/add horizontal lines
      {cmdab:noeqli:nes}{col 26}{...}
suppress lines between equations
      {cmd:compress}{col 26}{...}
reduce horizontal spacing
      {cmd:plain}{col 26}{...}
produce a minimally formatted table

    {help esttab##label:Labeling}
      {cmdab:l:abel}{col 26}{...}
make use of variable labels
      {cmdab:ti:tle:(}{it:string}{cmd:)}{col 26}{...}
specify a title for the table
      {cmdab:mti:tles:(}{it:list}{cmd:)}{col 26}{...}
specify model titles to appear in table header
      {cmdab:nomti:tles}{col 26}{...}
disable model titles
      [{ul:{cmd:no}}]{cmdab:dep:vars}{col 26}{...}
do not/do print dependent variables in header
      [{ul:{cmd:no}}]{cmdab:num:bers}{col 26}{...}
do not/do print model numbers in table header
      {cmdab:coef:labels:(}{it:list}{cmd:)}{col 26}{...}
specify labels for coefficients
      [{ul:{cmd:no}}]{cmdab:note:s}{col 26}{...}
suppress/add notes in the table footer
      {cmdab:addn:otes:(}{it:list}{cmd:)}{col 26}{...}
add lines at the end of the table

    {help esttab##format:Document format}
      {cmd:smcl} | {cmdab:fix:ed} | {cmd:tab} | {cmd:csv} | {cmdab:sc:sv} | {cmd:rtf} | {cmdab:htm:l} | {cmd:tex} | {cmdab:bookt:abs}
      {col 26}{...}
set the document format ({cmd:smcl} is the default)
      {cmdab:f:ragment}{col 26}{...}
suppress table opening and closing (LaTeX, HTML)
      {cmd:page}[{cmd:(}{it:packages}{cmd:)}]{col 26}{...}
add page opening and closing (LaTeX, HTML)
      {cmdab:align:ment(}{it:string}{cmd:)}{col 26}{...}
set alignment within columns (LaTeX, HTML, RTF)
      {cmdab:width(}{it:string}{cmd:)}{col 26}{...}
set width of table (LaTeX, HTML)

    {help esttab##output:Output}
      {cmdab:r:eplace}{col 26}{...}
overwrite an existing file
      {cmdab:a:ppend}{col 26}{...}
append the output to an existing file
      {cmdab:ty:pe}{col 26}{...}
force prining the table in the results window
      {cmdab:n:oisily}{col 26}{...}
display the executed {helpb estout} command

    {help esttab##advanced:Advanced}
      {cmdab:d:rop:(}{it:list}{cmd:)}{col 26}{...}
drop individual coefficients
      {cmdab:k:eep:(}{it:list}{cmd:)}{col 26}{...}
keep individual coefficients
      {cmdab:o:rder:(}{it:list}{cmd:)}{col 26}{...}
change order of coefficients
      {cmdab:eq:uations:(}{it:list}{cmd:)}{col 26}{...}
match the models' equations
      {cmd:eform}{col 26}{...}
report exponentiated coefficients
      {cmdab:m:argin}{col 26}{...}
report marginal effects/elasticities
      {cmdab:uns:tack}{col 26}{...}
place multiple equations in separate columns
      {it:estout_options}{col 26}{...}
any other {helpb estout} options
    {hline 70}


{marker des}{...}
{title:Description}

{p 4 4 2}
{cmd:esttab} is a wrapper for {helpb estout}. It produces a
pretty-looking publication-style regression table from stored
estimates without much typing. The compiled table is displayed in the
Stata results window or, optionally, written to a text file specified
by {cmd:using} {it:filename}. If {it:filename} is specified without
suffix, a default suffix is added depending on the specified document
format (".smcl" for {cmd:smcl}, ".txt" for {cmd:fixed} and {cmd:tab}, ".csv" for {cmd:csv}
and {cmd:scsv}, ".rtf" for {cmd:rtf}, ".html" for {cmd:html}, and
".tex" for {cmd:tex} and {cmd:booktabs}).

{p 4 4 2}
{it:namelist} provides the names of the stored estimation sets to be
tabulated. You may use the {cmd:*} and {cmd:?} wildcards in
{it:namelist}. If {it:namelist} is omitted, {cmd:esttab} tabulates the
estimation sets stored by {cmd:eststo} (see {helpb eststo})
or, if no such estimates are present, the currently active
estimates (i.e., the model fitted last).

{p 4 4 2}
See {helpb estimates} for information about storing estimation
results. An alternative to the {cmd:estimates store} command is
provided by {helpb eststo}.


{marker opt}{...}
{title:Options}

{marker main}{...}
{dlgtab:Main}

{p 4 8 2}
{cmd:b(}{it:{help esttab##fmt:fmt}}{cmd:)} sets the numerical display format
for the point estimates. The default format is {cmd:a3}. (See
{help esttab##fmt:Numerical formats} below for details on available
formats.)

{p 4 8 2}
{cmd:beta}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}] requests that
standardized beta coefficients be displayed in place of the raw point
estimates and, optionally, sets the display format (the default is to
print three decimal places). {cmd:beta} causes the
intercept to be dropped from the table (unless {cmd:constant} is
specified).

{p 4 8 2}
{cmd:main(}{it:name} [{it:{help esttab##fmt:fmt}}]{cmd:)} requests that
the statistics stored in {cmd:e(}{it:name}{cmd:)} be displayed in
place of the point estimates and, optionally, sets the display format
(the default is to use the display format for point estimates). For
example, {cmd:e(}{it:name}{cmd:)} may contain statistics added by
{cmd:estadd} (see {helpb estadd}).

{p 4 8 2}
{cmd:t(}{it:{help esttab##fmt:fmt}}{cmd:)} sets the display format for
t statistics. The default is to display two decimal places.

{p 4 8 2}
{cmd:abs} reports absolute values of t statistics.

{p 4 8 2}
{cmd:not} suppresses the printing of t statistics.

{p 4 8 2}
{cmd:se}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}] requests that
standard errors be displayed in place of t statistics and,
optionally, sets the display format (the default is to use the
display format for point estimates).

{p 4 8 2}
{cmd:p}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}] requests that
p-values be displayed in place of t statistics and, optionally, sets
the display format (the default is to print three decimal places)

{p 4 8 2}
{cmd:ci}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}] requests that
confidence intervals be displayed in place of t statistics and,
optionally, sets the display format (the default is to use the
display format for point estimates). {cmd:level(}{it:#}{cmd:)}
assigns the confidence level, as a percentage. The default is
{cmd:level(95)} or as set by {helpb set level}.

{p 4 8 2}
{cmd:aux(}{it:name} [{it:{help esttab##fmt:fmt}}]{cmd:)} requests that
the statistics stored in {cmd:e(}{it:name}{cmd:)} be displayed in
place of t statistics and, optionally, sets the display format (the
default is to use the display format for point estimates). For
example, {cmd:e(}{it:name}{cmd:)} may contain statistics added by
{cmd:estadd} (see {helpb estadd}, if installed).

{p 4 8 2}
{cmd:noconstant} drops the intercept from the table.
Specify {cmd:constant} to include the constant in situations where it
is dropped by default.

{marker sig}{...}
{dlgtab:Significance asterisks}

{p 4 8 2}
{cmd:star}[{cmd:(}{it:symbol} {it:level} [{it:...}]{cmd:)}] prints
asterisks denoting the significance of the coefficients 
next to the point estimates. This is the default. Type {cmd:nostar}
to suppress the asterisks. The default symbols and thresholds are:
{cmd:*} for p<.05, {cmd:**} for p<.01, and {cmd:***} for p<.001.
Alternatively, for example, type {bind:{cmd:star(+ 0.10 * 0.05)}} to
set the following thresholds: {cmd:+} for p<.10 and {cmd:*} for
p<.05. The thresholds must lie in the (0,1] interval and
must be specified in descending order.

{p 4 8 2}
{cmd:staraux} causes the significance asterisks be printed next to the
t statistics (or standard errors, etc.) instead of the point estimates.

{marker stat}{...}
{dlgtab:Summary statistics}

{p 4 8 2}
{cmd:r2}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}],
{cmd:ar2}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}], and
{cmd:pr2}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]
include the R-squared, the adjusted R-squared, and the
pseudo-R-squared in the table footer and, optionally, set the
corresponding display formats (the default is to display three
decimal places).

{p 4 8 2}
{cmd:aic}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}] and
{cmd:bic}[{cmd:(}{it:{help esttab##fmt:fmt}}{cmd:)}]
include Akaike's and Schwarz's Bayesian information criterion in the table
footer and, optionally, set the corresponding display formats (the
default is to use the display format for point estimates).

{p 4 8 2}
{cmd:scalars(}{it:list}{cmd:)} may be used to add other
{cmd:e()} scalars to the table footer (type {cmd:ereturn list} to
display a list of available {cmd:e()} scalars after fitting a model;
see {helpb ereturn}). For example, {cmd:scalars(df_m)} would
report the model degrees of freedom for each model. {it:list} may be
a simple list of names of {cmd:e()} scalars, e.g.,

            {cmd}. esttab, scalars(ll_0 ll chi2){txt}

{p 8 8 2}
or, alternatively, a list of quoted name-label pairs, e.g.,

            {cmd}. esttab, scalars({bind:"ll Log lik."} {bind:"chi2 Chi-squared"}){txt}

{p 4 8 2}
{cmd:sfmt(}{it:{help esttab##fmt:fmt}} [{it:...}]{cmd:)} sets the
display format(s) for the statistics specified in {cmd:scalars()}
(the default is to use the display format for point estimates). If
{cmd:sfmt()} contains less elements than {cmd:scalars()}, the last
specified format is used for the remaining scalars. That is, only one
format needs to be specified if the same format be used for all
scalars.

{p 4 8 2}
{cmd:noobs} suppresses displaying information on the number of
observations. The default is to report the number of observations for
each model in the table footer.

{p 4 8 2}
{cmd:obslast} displays the number of observations in the last row of
the table footer. The default is to use the first row.

{marker layout}{...}
{dlgtab:Layout}

{p 4 8 2}
{cmd:wide} causes point estimates and t statistics (or standard errors,
etc.) to be printed beside one another instead of beneath one another.

{p 4 8 2}
{cmd:parentheses} encloses t statistics (or standard errors, etc.) in
parentheses. This is the default. Specify {cmd:noparentheses} to
suppress the parentheses.

{p 4 8 2}
{cmd:brackets} uses square brackets, [], instead of parentheses. 
{cmd:brackets} is the default for confidence intervals.

{p 4 8 2}
{cmd:gaps} adds empty rows (or, more generally, vertical
space) between coefficients to increase readability (empty rows are
also inserted between the table's header, body, and footer, unless
{cmd:lines} is activated). This is the default unless {cmd:wide} or
{cmd:not} is specified. Type {cmd:nogaps} to suppress the extra
spacing.

{p 4 8 2}
{cmd:lines} adds horizontal lines to the table separating the table's
header, body, and footer and, for multiple 
equation models, the equations. This is the default. Specify {cmd:nolines}
to suppress the lines. Lines are always suppressed in the {cmd:tab}
and {cmd:csv} modes. 

{p 4 8 2}
{cmd:noeqlines} suppresses the horizontal lines between equations 
for multiple equation models.

{p 4 8 2}
{cmd:compress} reduces the amount of horizontal spacing (so that more
models fit on screen without line breaking). The option has no effect
in the {cmd:tab} and {cmd:csv} modes. Furthermore, in the
TeX and HTML modes the {cmd:compress} option changes only the
arrangement the table's code but not the look of the compiled
end product. In {cmd:rtf}, however, {cmd:compress} changes the look
of the formatted table.

{p 4 8 2}
{cmd:plain} produces a minimally formatted table. It is shorthand
for specifying {cmd:nostar}, {cmd:nodepvars}, {cmd:nonumbers},
{cmd:noparentheses}, {cmd:nogaps}, {cmd:nolines} and {cmd:nonotes}
and setting all formats to {cmd:%9.0g}. The disabled
options can be switched on again. For example, type

            {cmd}. esttab, plain star{txt}

{p 8 8 2}
to produce a plain table including significance asterisks.

{marker label}{...}
{dlgtab:Labeling}

{p 4 8 2}
{cmd:label} specifies that variable labels be used instead of
variable names (and estimation set titles be used instead of
estimation set names). Furthermore, {cmd:label} prints "Constant"
instead of "_cons".

{p 4 8 2}
{cmd:title(}{it:string}{cmd:)} may be used to provide a title for the
table. If specified, {it:string} is printed at the top of the table.
Specifying a title causes the table to be set up as a
floating object in LaTeX mode. You may want to set a label for
referencing in this case. For example, if you type
{cmd:title(...\label{tab1})}, then "\ref{tab1}" could be used in the
LaTeX document to point to the table.

{p 4 8 2}
{cmd:mtitles(}{it:list}{cmd:)} specifies a list of model titles to
appear above the models' columns in the table header. Enclose titles
in double quotes if they contain spaces,
e.g., {cmd:mtitles({bind:"Model 1"} {bind:"Model 2"})}. The default is
to print the name (or label) of the (first) dependent variable of a
model as the model's title.

{p 4 8 2}
{cmd:nomtitles} suppresses printing of model titles.

{p 4 8 2}
{cmd:depvars} prints the name (or label) of the (first) dependent
variable of a model as the model's title in the table header. This is
the default. Specify {cmd:nodepvars} to use the names (or titles) of
the stored estimation sets instead of the variables.

{p 4 8 2}
{cmd:numbers} includes a row containing consecutive model numbers in
the table header. This is the default. Specify {cmd:nonumbers} to
suppress printing the model numbers.

{p 4 8 2}
{cmd:coeflabels(}{it:name} {it:label} [...]{cmd:)} specifies labels
for the coefficients. Specify names and labels in pairs and, if
necessary, enclose labels in double quotes,
e.g., {cmd:coeflabels(mpg Milage rep78 {bind:"Repair Record"})}.

{p 4 8 2}
{cmd:notes} prints notes at the end of the table explaining the
significance symbols and the type of displayed statistics. This is
the default. Specify {cmd:nonotes} to suppress the notes.

{p 4 8 2}
{cmd:addnotes(}{it:list}{cmd:)} may be used to add further lines of
text at the bottom of the table. Lines containing blanks must be
enclosed in double quotes,
e.g., {cmd:addnotes({bind:"Line 1"} {bind:"Line 2"})}.

{marker format}{...}
{dlgtab:Document format}

{p 4 8 2}
{cmd:smcl}, {cmd:fixed}, {cmd:tab}, {cmd:csv},  {cmd:scsv}, {cmd:rtf},
{cmd:html}, {cmd:tex}, and {cmd:booktabs} choose the table's basic
output format. The default format is {cmd:smcl} unless 
{cmd:using} is specified, in which case the default format
depends on the filename's suffix ({cmd:smcl} for ".smcl", {cmd:csv} 
for ".csv", {cmd:rtf} for ".rtf", 
{cmd:html} for ".htm" or ".html", {cmd:tex} for ".tex", and {cmd:fixed}
for all other filenames).

{p 8 8 2}
{cmd:smcl} produces a {help SMCL}-formatted table to be displayed in the 
Stata Results window or the Stata viewer.

{p 8 8 2}
{cmd:fixed} produces a fixed-format ASCII table. This is suitable,
for example, if the table is to be displayed in a text editor.

{p 8 8 2}
{cmd:tab} produces a tab-delimited ASCII table.

{p 8 8 2}
{cmd:csv} produces a comma-separated value (CSV)-format table for use with
Microsoft Excel. Delimiter is a comma. To prevent Excel from
interpreting the contents of the table cells, they are enclosed in double
quotes preceded by an equal sign (i.e., ="..."). However, if the {cmd:plain}
option is specified, the table cells are enclosed in double quotes without the
leading equal sign. The first method is appropriate if you want to preserve
the table's formatting. The second method is appropriate if you want to use
the table's contents for more computations in Excel.

{p 8 8 2}
{cmd:scsv} is a variant on the CSV format that uses a semicolon as
the delimiter. This is appropriate for some non-English versions of
Excel (e.g., the German version).

{p 8 8 2}
{cmd:rtf} produces a rich-text format table for use with word
processors.

{p 8 8 2}
{cmd:html} produces a simple HTML-formatted table.

{p 8 8 2}
{cmd:tex} produces a LaTeX-formatted table.

{p 8 8 2}
{cmd:booktabs} produces a LaTeX-formatted table for use with LaTeX's
{it:booktabs} package.

{p 4 8 2}
{cmd:fragment} suppresses the table's opening and closing specifications
to be suppressed. This is relevant primarily in LaTeX and HTML mode.

{p 4 8 2}
{cmd:page}[{cmd:(}{it:packages}{cmd:)}] adds opening and closing code
to define a whole LaTeX or HTML document. The default is to produce a
raw table that can then be included into an existing LaTeX or HTML
document. Specifying {it:packages} in parentheses adds 
{cmd:\usepackage{c -(}}{it:packages}{cmd:{c )-}} to the
preamble of the LaTeX document (the {it:booktabs} package
is automatically loaded if {cmd:booktabs} is specified).

{p 4 8 2}
{cmd:alignment(}{it:string}{cmd:)} may be used to specify the
alignment of the models' columns in LaTeX, HTML, or RTF mode.

{p 8 8 2}
In LaTeX mode, {it:string} should be a LaTeX column specifier. The
default is to center the columns. To produce right-aligned columns,
for example, type {cmd:alignment(r)}. If the table contains multiple
columns per model/equation, the alignment specification should define
all columns. For example, if the {cmd:wide} option is specified, you
could type {cmd:alignment(cr)} to, say, center the point estimates
and right-align the t statistics. More sophisticated column
definitions are often needed to produce appealing results. In
particular, LaTeX's {it:dcolumn} package proves useful to align
columns on the decimal point.

{p 8 8 2}
In HTML mode {it:string} should be a HTML alignment specifier. The
default is to omit alignment specification, which results in left-aligned
columns. To center the columns in HTML, for example, specify
{cmd:alignment(center)}. Other than in LaTeX mode, the same alignment is used
for all columns if the table contains multiple columns per model/equation in
the HTML mode.

{p 8 8 2}
In RTF mode, {it:string} should be one of {cmd:l}, {cmd:c}, {cmd:r},
and {cmd:j}. The default is to center the columns. To produce
right-aligned columns, for example, type {cmd:alignment(r)}. The same
alignment is used for all columns if the table contains multiple
columns per model/equation in RTF mode.

{p 8 8 2}
{cmd:alignment()} does not change the alignment of the
variable names/labels in the left stub of the table. They are always
left-aligned.

{p 4 8 2}
{cmd:width(}{it:string}{cmd:)} sets the overall width of the table in
LaTeX or HTML. {it:string} should be LaTeX or HTML literal. For
example, specify {cmd:width(\hsize)} in LaTeX or {cmd:width(100%)} in
HTML to span the whole page. The table columns will spread regularly
over the specified width. In RTF mode, {helpb estout}'s
{cmd:varwidth()} and {cmd:modelwidth()} options may be used to change
the width of the table columns.

{marker output}{...}
{dlgtab:Output}

{p 4 8 2}
{cmd:replace} permits {cmd:esttab} to overwrite an existing file.

{p 4 8 2}
{cmd:append} specifies that the output be appended to an existing
file. It may be used even if the file does not yet exist. Specifying
{cmd:append} together with {cmd:page} in TeX or HTML mode causes the
new table to be inserted at the end of the body of an existing
document ({cmd:esttab} seeks a line reading "\end{document}" or
"</body>", respectively, and starts appending from there;
contents after this line will be overwritten). In RTF mode, existing
documents are assumed to end with a line containing a single "}".

{p 4 8 2}
{cmd:type} specifies that the assembled table be printed in the
results window and the log file. This is the default unless
{cmd:using} is specified.

{p 4 8 2}
{cmd:noisily} displays the executed {helpb estout} command.

{marker advanced}{...}
{dlgtab:Advanced}

{p 4 8 2}
{cmd:drop(}{it:droplist}{cmd:)} identifies the coefficients to be
dropped from the table. A {it:droplist} comprises one or more
specifications, separated by white space. A specification can be
either a parameter name (e.g., {cmd:price}), an equation name followed
by a colon (e.g., {cmd:mean:}), or a full name
(e.g., {cmd:mean:price}). You may use the {cmd:*} and {cmd:?} wildcards
in equation names and parameter names. Be sure to refer to the matched
equation names, and not to the original equation names in the models,
when using the {cmd:equations()} option to match equations.

{p 4 8 2}
{cmd:keep(}{it:keeplist}{cmd:)} selects the coefficients to be
included in the table. {it:keeplist} is specified analogous to
{it:droplist} in {cmd:drop()} (see above).

{p 4 8 2}
{cmd:order(}{it:orderlist}{cmd:)} changes the order of the
coefficients and equations within the table. {it:orderlist} is
specified analogous to {it:droplist} in {cmd:drop()} (see above).
Coefficients and equations that do not appear in {it:orderlist} are
placed last (in their original order).

{p 4 8 2}
{cmd:equations(}{it:eqmatchlist}{cmd:)} specifies how the models'
equations are to be matched. This option is passed to the internal
call of {cmd:estimates table}. See {helpb estimates} on how to
specify this option. The most common usage is {cmd:equations(1)} to
match all the first equations in the models.

{p 4 8 2}
{cmd:eform} displays the regression table in exponentiated form. The
exponent of a coefficient is displayed in lieu of the untransformed
coefficient; standard errors and confidence intervals are transformed
as well. The intercept is dropped in eform mode, unless
{cmd:constant} is specified.

{p 4 8 2}
{cmd:margin} indicates that the marginal effects or elasticities be
reported instead of the raw coefficients. A prerequisite for this
option to work correctly is that {cmd:mfx} has been applied to a
model prior to storing its results (see {helpb mfx}). 
The standard errors, etc., are transformed as well. Furthermore, the
intercept is dropped, unless {cmd:constant} is specified.

{p 4 8 2}
{cmd:unstack} specifies that the individual equations from
multiple-equation models (e.g., {cmd:mlogit}, {cmd:reg3},
{cmd:heckman}) be placed in separate columns. The default is to place
the equations below one another in a single column.

{p 4 8 2}
{it:estout_options} are any other {cmd:estout} options (see help
{helpb estout}). {cmd:estout} options take precedence over
{cmd:esttab} options. For example,

{p 8 20 2}
{cmd:cells()}{space 5}disables {cmd:b()}, {cmd:beta()}, {cmd:main()},
{cmd:t()}, {cmd:abs}, {cmd:not}, {cmd:se()}, {cmd:p()}, {cmd:ci()},
{cmd:aux()}, {cmd:star}, {cmd:staraux}, {cmd:parentheses}, and
{cmd:brackets},

{p 8 20 2}
{cmd:stats()}{space 5}disables {cmd:r2()}, {cmd:ar2()}, {cmd:pr2()},
{cmd:aic()}, {cmd:bic()}, {cmd:scalars()}, {cmd:sfmt()}, {cmd:noobs},
and {cmd:obslast}.

{p 8 8 2}
Other {cmd:estout} options that should be used with care are
{cmd:begin()}, {cmd:delimiter()}, {cmd:end()}, {cmd:prehead()},
{cmd:posthead()}, {cmd:prefoot()}, {cmd:postfoot()}, {cmd:mlabels()},
and {cmd:varlabels()}.

{marker fmt}{...}
{dlgtab:Numerical formats}

{p 4 4 2}
Numerical display formats may be specified in {cmd:esttab} as follows:

{p 5 8 2}
1. Official Stata's display formats: You may specify formats, such as
{cmd:%9.0g} or {cmd:%8.2f}. See {helpb format} for a list
of available formats.

{p 5 8 2}
2. Fixed format: You may specify an integer value such as {cmd:0},
{cmd:1}, {cmd:2}. to request a display format with a fixed number
of decimal places. For example, {cmd:t(3)} would display t statistics
with three decimal places.

{p 5 8 2}
3. Automatic format: You may specify {cmd:a1}, {cmd:a2}, ..., or
{cmd:a9} to cause {cmd:esttab} to choose a reasonable display format for
each number depending on the number's value. The {it:#} in
{cmd:a}{it:#} determines the minimum precision according to the
following rules:

{p 10 12 2}
o Absolute numbers smaller than 1 are displayed with {it:#}
significant decimal places (i.e., with {it:#} decimal places ignoring
any leading zeros after the decimal point). For example,
{cmd:0.00123456} is displayed as {cmd:0.00123} if the format is
{cmd:a3}.

{p 10 12 2}
o Absolute numbers greater than 1 are displayed with as many digits
required to retain at least one decimal place and are displayed with
a minimum of ({it:#} + 1) digits. For example, if the format is
{cmd:a3}, {cmd:1.23456} is displayed as {cmd:1.235}, {cmd:12.3456} is
displayed as {cmd:12.35}, and {cmd:1234.56} is displayed as
{cmd:1234.6}.

{p 10 12 2}
o In any case, integers are displayed with zero decimal places, and
very large or very small absolute numbers are displayed in
exponential format.


{marker exa}{...}
{title:Examples}

{p 4 4 2}
The following examples show the basic usage of {cmd:esttab}. The procedure is
to first fit and store some models and then apply {cmd:esttab} to these stored
estimates:

        {cmd}. sysuse auto
        {cmd}. quietly regress price weight mpg
        {cmd}. estimates store model1
        {cmd}. quietly regress price weight mpg foreign
        {cmd}. estimates store model2
        {cmd}. esttab *, ar2
        {txt}{...}

{p 4 4 2}
The same table using labels:

        {cmd}. esttab *, ar2 label
        {txt}{...}

{p 4 4 2}
Plain table:

        {cmd}. esttab *, ar2 plain
        {txt}{...}

{p 4 4 2}
Using standard errors in brackets and suppress significance asterisks:

        {cmd}. esttab *, se nostar brackets
        {txt}{...}

{p 4 4 2}
Printing beta coefficients:

        {cmd}. esttab *, beta
        {txt}{...}


{marker ret}{...}
{title:Saved results}

{p 4 4 2}
{cmd:esttab} returns in macro {cmd:r(estout)} a copy of the executed
{helpb estout} command (typing {cmd:`r(estout)'} after {cmd:esttab}
reproduces the table).


{marker aut}{...}
{title:Author}

{p 4 4 2}
Ben Jann, ETH Zurich, jann@soz.gess.ethz.ch


{marker als}{...}
{title:Also see}

    Manual:  {hi:[R] estimates}

        SJ:  SJ5-3 st0085 (Jann 2005)

{p 4 13 2}Online:  
 {helpb estimates},
 {help estcom},
 {helpb estout},
 {helpb estadd},
 {helpb eststo}
{p_end}
